cmake_minimum_required(VERSION 3.27.6)

project(@content_name@-install LANGUAGES NONE)
set(HERMETIC_FETCHCONTENT_ROOT_DIR "@HERMETIC_FETCHCONTENT_ROOT_DIR@")
list(APPEND CMAKE_MODULE_PATH "${HERMETIC_FETCHCONTENT_ROOT_DIR}/modules")

include(ExternalProject)
include(hfc_goldilock_helpers)

set(CMAKE_PROGRAM_PATH "@CMAKE_PROGRAM_PATH@") # inherit the CMAKE_PROGRAM_PATH of the project being configured/generating this file
set(HERMETIC_FETCHCONTENT_goldilock_BIN "@HERMETIC_FETCHCONTENT_goldilock_BIN@")

set(input_SOURCE_DIR "@TEMPLATE_SOURCE_DIR@")
set(input_BINARY_DIR "@TEMPLATE_BINARY_DIR@")
set(input_INSTALL_DIR  "@TEMPLATE_INSTALL_DIR@")

set(goldilock_commands "${HERMETIC_FETCHCONTENT_goldilock_BIN} ")

function(goldilock_command_append_lockfile dir)
  __get_lockfile_path("${dir}" lockfile)
  set(goldilock_commands "${goldilock_commands} --lockfile ${lockfile}" PARENT_SCOPE)
endfunction()


goldilock_command_append_lockfile("${input_SOURCE_DIR}")
set(effective_source_dir "${input_SOURCE_DIR}")

goldilock_command_append_lockfile("${input_INSTALL_DIR}")

if(NOT "${effective_source_dir}" STREQUAL "${input_BINARY_DIR}")
  goldilock_command_append_lockfile("${input_BINARY_DIR}")
endif()

# this is going to be interpreted by bash and will use our getcpujobcount.cmake script to
# either "use the value of CMAKE_BUILD_PARALLEL_LEVEL or fall back to number of CPUs detected by cmake"
#
# note that ${variable} will be interpreted by cmake, however the surrounding $(...) (note the parentheses vs curly bracket!)
# are bash syntax and will be executed at runtime by the bash that is to be found in the command_str below
set(NUM_JOB_PLACEHOLDER "$(${CMAKE_COMMAND} -P ${HERMETIC_FETCHCONTENT_ROOT_DIR}/scripts/getcpujobcount.cmake)")
string(CONFIGURE "@TEMPLATE_BUILD_CMD@" configure_command @ONLY)

# build the command (with explanations)
set(command_str "")
string(APPEND command_str     "test -d ${effective_source_dir}")  # fail is the source dir is not here for some reason
string(APPEND command_str " && ${goldilock_commands} -- /bin/bash -c \"${configure_command} && @TEMPLATE_INSTALL_CMD@\"")     # acquire all locks

ExternalProject_Add(@TEMPLATE_TARGET_NAME@
  SOURCE_DIR "@TEMPLATE_SOURCE_DIR@"
  BINARY_DIR "@TEMPLATE_BINARY_DIR@"
  INSTALL_DIR "@TEMPLATE_INSTALL_DIR@"

  CONFIGURE_COMMAND ""
  UPDATE_COMMAND ""  
  BUILD_COMMAND ""  
  INSTALL_COMMAND /bin/bash -c "${command_str}"

  INSTALL_BYPRODUCTS "@TEMPLATE_INSTALL_BYPRODUCTS@"
  DEPENDS "@TEMPLATE_DEPENDENCIES@"
)